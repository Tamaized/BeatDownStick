buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "1.19-${modVersion}${modVersionValue}"
group = "tamaized.beatdownstick"
archivesBaseName = "BeatDownStick"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

import net.minecraftforge.gradle.common.util.RunConfig

class DefaultRunConfig extends RunConfig {

    DefaultRunConfig(Project project, SourceSet sourceSet, String n) {
        super(project, n)
        this.taskName "Minecraft_" + name.substring(0, 1).toUpperCase(Locale.US).concat(name.substring(1)) + "_1.19"
        this.properties 'forge.logging.console.level': 'debug'
        this.properties 'mixin.env.disableRefMap': 'true'
        this.workingDirectory project.file('run').canonicalPath
        this.source sourceSet
        this.environment 'TMP': './tmp/'
        this.ideaModule project.getName().concat(".main")
        this.singleInstance true
    }

    DefaultRunConfig setArgs(String... args) {
        setArgs(new ArrayList() {
            {
                for (String arg : args)
                    add(arg)
            }
        })
        return this
    }
}

minecraft {
    mappings("${project.mappingType}", "${project.mappingVersion}")
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    var proj = project
    var main = sourceSets.main
    runs {
        add(new DefaultRunConfig(proj, main, "client").setArgs(

                '--username', 'Tamaized',

                '--uuid', '16fea09e-314e-4955-88c2-6b552ecf314a'

        ))

        add(new DefaultRunConfig(proj, main, "server"))
    }
}

repositories {
    maven {
        url = "https://dvs1.progwml6.com/files/maven/" // JEI
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.minecraftVersion}-${project.forgeVersion}"

    implementation fg.deobf("mezz.jei:${project.jeiVersion}")
}

processResources {
    filesMatching("META-INF/mods.toml") {
        expand([
                'version': "${project.version}"
        ])
    }
}

jar {
    manifest {
        attributes(["Specification-Title"     : "BeatDownStick",
                    "Specification-Vendor"    : "Tamaized",
                    "Specification-Version"   : "${modVersion}${modVersionValue}",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${version}",
                    "Implementation-Vendor"   : "Tamaized",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}